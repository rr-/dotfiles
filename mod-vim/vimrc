set nocompatible
filetype off
set rtp+=~/.vim/vundle/
call vundle#begin()
Plugin 'gmarik/Vundle.vim'                "better plugin manager
Plugin 'wincent/command-t'                "open files using fuzzy matching
Plugin 'kien/ctrlp.vim'                   "fallback alternative for command-t
Plugin 'scrooloose/nerdtree'              "file explorer sidebar
Plugin 'Lokaltog/vim-easymotion'          "move to any character!
Plugin 'vim-airline/vim-airline'          "riced status bar
Plugin 'vim-airline/vim-airline-themes'   "riced status bar - themes
Plugin 'Yggdroot/indentLine'              "vertical bars showing indent level
Plugin 'flazz/vim-colorschemes'           "color schemes!
Plugin 'octol/vim-cpp-enhanced-highlight' "improved C++11 highlighting
Plugin 'embear/vim-localvimrc'            "local vimrc
Plugin 'ntpeters/vim-better-whitespace'   "hilight and strip trailing whitespace
Plugin 'tpope/vim-rsi'                    "readline shortcuts in command mode
Plugin 'airblade/vim-gitgutter'           "show changed lines in git on margin
call vundle#end()

"-------------------------------------------------
" basic settings
"-------------------------------------------------
syntax on                     "enable syntax highlighting
set synmaxcol=256             "highlight up to X columns (binary files)
set number                    "enable line numbers

"color scheme
if $LIGHTNESS ==? 'light'     "$LIGHTNESS exported by zshrc
  colorscheme hemisu
  set background=light
else
  colorscheme sorcerer
  set background=dark
endif

"set gutter width to 80 characters
if exists('+colorcolumn')
  set colorcolumn=80
endif

"gui specific options
if has('gui_running')
  colorscheme winter
  set guioptions-=m             "remove menu bar
  set guioptions-=T             "remove toolbar
  set guioptions-=r             "remove right-hand scroll bar
  set guioptions-=L             "remove left-hand scroll bar
  set guifont=Liberation\ Mono\ 9
endif

"-----------------------------------------
" whitespace settings
"-----------------------------------------
set enc=utf-8                 "character encoding of gui
set fencs=utf-8,cp932         "preferred character encodings
set ff=unix ffs=unix,dos      "preferred eol styles
set expandtab                 "use spaces instead of tab
set tabstop=4                 "spaces that tab counts for
set softtabstop=4             "spaces that tab counts for in edit mode
set shiftwidth=4              "spaces for each step of auto-indent
set autoindent                "auto indentation
set wrapscan                  "search again from top if no matches
set nowrap                    "don't wrap long lines
filetype on                   "enable setting options based on file

"----------------------------------------
" editor behavior
"----------------------------------------
set bs=indent,eol,start       "allow backspacing over indent, eol and ?
set virtualedit=onemore       "allow moving cursor up to EOL+1 character
set splitbelow splitright     "change placement when splitting a buffer
set shell=/bin/zsh            "when opening shell, use zsh
set mouse=a                   "enable mouse support
set noincsearch               "disable "live" search
set hlsearch                  "highlight searches
set ignorecase                "ignore case in searches
set smartcase                 " ...unless they contain uppercase chars
set scrolloff=5               "keep at least x lines below and above cursor
set nojoinspaces              "don't put double spaces when using auto wrapping
set fillchars=vert:\│         "better vertical fill character

"----------------------------------------
" miscellaneous
"----------------------------------------
set noeb vb t_vb=             "disable beeping
set laststatus=2              "always display status line
set modeline                  "allow files to embed file-specific vim settings
set modelines=5               " ...within X lines at the top of that file
set nospell                   "spell checker is off by default
set spelllang=en_us,pl        "spell checker languages
set spellfile=~/.vim/spell/en.utf-8.add,~/.vim/spell/pl.utf-8.add

"------------------------------------------
" storage
"------------------------------------------
"double slash prevents file name collision, by using full file paths
set viminfo+=n~/.vim/viminfo       "set path to last session data storage
set backupdir=~/.vim/backup//      "set path to file backups
set backup                         "enable backups
set directory=~/.vim/swap//        "set path to swap files (.*.swp)
set undodir=~/.vim/undo//          "set path to undo data file
set undofile
set undolevels=1000
set undoreload=10000

"------------------------------------------
" ignore files in commands and plugins
"------------------------------------------
"list of files to ignore
set wildignore+=.hg,.git,.bzr
set wildignore+=*.o,*.obj
set wildignore+=*.pyc,*.pyo,__pycache__
set wildignore+=*.swp,*.spl
set wildignore+=*.stackdump
set wildignore+=*~.*
set wildignorecase            "case-insensitive filename completion in commands

"----------------------------------------
" highlight bad whitespace
"----------------------------------------
"highlight color for bad whitespace
highlight SpecialKey ctermbg=NONE ctermfg=187
"fix ColorColumn on dark backgrounds
if &background == 'dark'
  highlight ColorColumn ctermbg=235
else
  highlight Normal ctermbg=NONE
  highlight ColorColumn ctermbg=255 guibg=#ddaaaa
end
"show white characters
set list listchars=tab:→\ ,trail:·

"----------------------------------------
" custom commands
"----------------------------------------
"fast indentation styles for working with other people's projects
command! TwoSpaces set et sts=2 ts=2 sw=2
command! FourSpaces set et sts=4 ts=4 sw=4
command! Tabs set noet sts=4 ts=4 sw=4

"decimal ↔ hexadecimal conversions
command! -nargs=? -range Dec2hex call s:Dec2hex(<line1>, <line2>, '<args>')
function! s:Dec2hex(line1, line2, arg) range
  if empty(a:arg)
    if histget(':', -1) =~# "^'<,'>" && visualmode() !=# 'V'
      let cmd = 's/\%V\<\d\+\>/\=printf("0x%X",submatch(0)+0)/g'
    else
      let cmd = 's/\<\d\+\>/\=printf("0x%X",submatch(0)+0)/g'
    endif
    try
      execute a:line1 . ',' . a:line2 . cmd
    catch
      echo 'Error: No decimal number found'
    endtry
  else
    echo printf('%X', a:arg + 0)
  endif
endfunction
command! -nargs=? -range Hex2dec call s:Hex2dec(<line1>, <line2>, '<args>')
function! s:Hex2dec(line1, line2, arg) range
  if empty(a:arg)
    if histget(':', -1) =~# "^'<,'>" && visualmode() !=# 'V'
      let cmd = 's/\%V0x\x\+/\=submatch(0)+0/g'
    else
      let cmd = 's/0x\x\+/\=submatch(0)+0/g'
    endif
    try
      execute a:line1 . ',' . a:line2 . cmd
    catch
      echo 'Error: No hex number starting "0x" found'
    endtry
  else
    echo (a:arg =~? '^0x') ? a:arg + 0 : ('0x'.a:arg) + 0
  endif
endfunction

"----------------------------------------
" config localvimrc
"----------------------------------------
let g:localvimrc_persistent = 1         "remember decisions to load given lvimrc

"----------------------------------------
" config ctrl+p
"----------------------------------------
let g:ctrlp_working_path_mode = 'c'     "disable VCS dir discovery and traversal
let g:ctrlp_dotfiles = 1                "enable matching files such as .htaccess
let g:ctrlp_match_window_bottom = 1     "show selected element at the top
let g:ctrlp_match_window_reversed = 1   "show list top->bottom
let g:ctrlp_by_filename = 1             "fuzzy match considers only file names
let g:ctrlp_match_window = 'bottom,order:btt,min:1,max:30,results:30'

"----------------------------------------
" config command-t
"----------------------------------------
let g:CommandTMaxHeight = 30            "limit window height to this many lines
let g:CommandTMatchWindowReverse = 1    "show matches near the bottom
let g:CommandTWildIgnore = &wildignore . ",**/data/posts/*,**/data/thumbnails/*,**/node_modules/*,**/python_modules/*,**/build/*"

"----------------------------------------
" config airline
"----------------------------------------
"because of white background
if &background == 'dark'
  let g:airline_theme = 'ubaryd'
else
  let g:airline_theme = 'sol'
endif
"allow use of special characters that are supplied by terminal font
let g:airline_powerline_fonts = 1
"enable tabs
let g:airline#extensions#tabline#enabled = 1
"don't show buffers in the tabline - show tabs instead
let g:airline#extensions#tabline#show_buffers = 0
"show tab number near tab for easy navigation with :![number]gt
let g:airline#extensions#tabline#tab_nr_type = 1
"don't show splits
let g:airline#extensions#tabline#show_splits = 0

"----------------------------------------
" config gitgutter
"----------------------------------------
let g:gitgutter_sign_column_always = 1
set updatetime=250

"----------------------------------------
" config NERDtree
"----------------------------------------
"f2 = disable NERDtree
autocmd VimEnter * inoremap <F2> <esc>:NERDTreeToggle<CR>
autocmd VimEnter * nnoremap <F2> :NERDTreeToggle<CR>
"f3 = find current file in nerdtree
autocmd VimEnter * inoremap <F3> <esc>:NERDTreeFind<CR>
autocmd VimEnter * nnoremap <F3> :NERDTreeFind<CR>
"explicitly disable some things in NERDTree - seems like wildignore glitches
let NERDTreeIgnore=['__pycache__','vendor','build/']

"----------------------------------------
" file-type specific settings
"----------------------------------------

"correct .lst filetype from assembler to text
autocmd BufRead,BufNewFile *.lst set filetype=text

"strip trailing whitespace for common source code
autocmd FileType c,cc,cxx,cpp,h,hpp,java,php,python,ruby,vim autocmd BufWritePre <buffer> :StripWhitespace

"enable spellcheck and hard wrapping for text files
autocmd FileType text,markdown setlocal spell textwidth=79

"enable spellcheck and double gutter in git commit messages
autocmd FileType gitcommit setlocal spell textwidth=72 colorcolumn=50,72

"disable usual sanity checkers for help, which is usually read-only
autocmd FileType help setlocal nospell | highlight clear ExtraWhitespace

"disable syntax for certain files
autocmd FileType text,xml,json setlocal syntax=

"setup indentation for common file types
autocmd FileType text     FourSpaces
autocmd FileType plaintex FourSpaces
autocmd FileType markdown FourSpaces
autocmd FileType c        FourSpaces
autocmd FileType cpp      FourSpaces
autocmd FileType python   FourSpaces
autocmd FileType lua      FourSpaces
autocmd FileType ruby     TwoSpaces
autocmd FileType vim      TwoSpaces
autocmd FileType zsh      FourSpaces
autocmd FileType sh       FourSpaces
autocmd FileType make     Tabs
autocmd FileType php      FourSpaces
autocmd FileType js       FourSpaces
autocmd FileType css      FourSpaces

"automatically sort word lists and generate spell files
autocmd BufWritePre */spell/*.add %sort i
autocmd BufWritePost */spell/*.add silent mkspell! %

"----------------------------------------
" custom keyboard bindings
"----------------------------------------
"set the leader key to -
let mapleader = "-"
"movement over visual lines, not physical lines
map <buffer> <silent> k gk
map <buffer> <silent> j gj
"ctrl+tab, ctrl+shift+tab = move to next/prev tab
nnoremap <Esc>[1;5I gt
nnoremap <Esc>[1;6I gT
nnoremap <C-Tab> gt
nnoremap <C-S-Tab> gT
"ctrl+t = open new tab
inoremap <silent> <C-t> <Esc>:tabnew<CR>
nnoremap <silent> <C-t> :tabnew<CR>
"ctrl+s = save
inoremap <silent> <C-s> <Esc>:update<CR>
nnoremap <silent> <C-s> :<C-u>update<CR>
"ctrl+z = open shell
nnoremap <silent> <C-z> :sh<CR>
"ctrl+q = close window
inoremap <silent> <C-q> <Esc>:q<CR>
nnoremap <silent> <C-q> :q<CR>
"ctrl+w ctrl+m = open new file vertically
nnoremap <silent> <C-w><C-m> :vne<CR>
nnoremap <silent> <C-w>m :vne<CR>
"make motion search easier to access
nmap <leader>z <Plug>(easymotion-s)
"save with sudo
cnoremap w!! w !sudo tee >/dev/null %
"disable stupid manual pages
nnoremap <silent> <S-K> <nop>
vnoremap <silent> <S-K> <nop>
"easier formatting of paragraphs
vnoremap Q gq
nnoremap Q gqap

  "----------------------------------------
  " interchange command-t and ctrl-p
  "----------------------------------------
  let g:ctrlp_map = '<nop>'
  function! PortableFuzzyOpen()
    if exists(':CommandT') && has('ruby')
      execute ':CommandT'
    elseif exists(':CtrlP')
      execute ':CtrlP'
    else
      echo 'Both CtrlP and CommandT are missing!'
    endif
  endfunction
  function! PortableFuzzyOpenMRU()
    if exists(':CommandT') && has('ruby')
      execute ':CommandTMRU'
    elseif exists(':CtrlPMRU')
      execute ':CtrlPMRU'
    else
      echo 'Both CtrlP and CommandT are missing!'
    endif
  endfunction
  nnoremap <Leader>t :call PortableFuzzyOpen()<CR>
  nnoremap <C-p> :call PortableFuzzyOpen()<CR>
  nnoremap <C-m> :call PortableFuzzyOpenMRU()<CR>

  "----------------------------------------
  " ctrl+k/j = move line up/down
  "----------------------------------------
  function! MoveLineUp()
    call MoveLineOrVisualUp(".", "")
  endfunction

  function! MoveLineDown()
    call MoveLineOrVisualDown(".", "")
  endfunction

  function! MoveVisualUp()
    call MoveLineOrVisualUp("'<", "'<,'>")
    normal gv
  endfunction

  function! MoveVisualDown()
    call MoveLineOrVisualDown("'>", "'<,'>")
    normal gv
  endfunction

  function! MoveLineOrVisualUp(line_getter, range)
    let l_num = line(a:line_getter)
    if l_num - v:count1 - 1 < 0
        let move_arg = "0"
    else
        let move_arg = a:line_getter." -".(v:count1 + 1)
    endif
    call MoveLineOrVisualUpOrDown(a:range."move ".move_arg)
  endfunction

  function! MoveLineOrVisualDown(line_getter, range)
    let l_num = line(a:line_getter)
    if l_num + v:count1 > line("$")
        let move_arg = "$"
    else
        let move_arg = a:line_getter." +".v:count1
    endif
    call MoveLineOrVisualUpOrDown(a:range."move ".move_arg)
  endfunction

  function! MoveLineOrVisualUpOrDown(move_arg)
    let col_num = virtcol(".")
    execute "silent! ".a:move_arg
    execute "normal! ".col_num."|"
  endfunction

  nnoremap <silent> <C-k> :<C-u>call MoveLineUp()<CR>
  nnoremap <silent> <C-j> :<C-u>call MoveLineDown()<CR>
  vnoremap <silent> <C-k> :<C-u>call MoveVisualUp()<CR>
  vnoremap <silent> <C-j> :<C-u>call MoveVisualDown()<CR>
  inoremap <silent> <C-k> <Esc>:call MoveLineUp()<CR>a
  inoremap <silent> <C-j> <Esc>:call MoveLineDown()<CR>a
